/*
  â•”â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•—
  â•‘                 Projeto: SafeGuard e 7life                 â•‘
  â•‘                  Plataforma: ESP32 + MQTT                  â•‘
  â•‘------------------------------------------------------------â•‘
  â•‘  DescriÃ§Ã£o:                                                â•‘
  â•‘  CÃ³digo desenvolvido para ler sensores de batimentos,      â•‘
  â•‘  oxigenaÃ§Ã£o (SpO2) e temperatura corporal. Os dados sÃ£o    â•‘
  â•‘  exibidos em um display OLED e enviados via MQTT para um   â•‘
  â•‘  servidor central para monitoramento remoto.               â•‘
  â•‘                                                            â•‘
  â•‘  Funcionalidades:                                          â•‘
  â•‘   - Leitura dos sensores MAX30102 e MLX90614 via I2C       â•‘
  â•‘   - ExibiÃ§Ã£o dos dados em display SSD1306                  â•‘
  â•‘   - ComunicaÃ§Ã£o via Wi-Fi + MQTT (PubSubClient)            â•‘
  â•‘   - SincronizaÃ§Ã£o com horÃ¡rio NTP                          â•‘
  â•‘                                                            â•‘
  â•‘  Autor: grupo7zipado                                       â•‘
  â•‘  Curso: TÃ©cnico em Redes de Computadores                   â•‘
  â•‘  InstituiÃ§Ã£o: ETEC TupÃ£ Prof. Massuyuki Kawano             â•‘
  â•‘  Github: github.com/grupo7zipado                           â•‘
  â•‘                                                            â•‘
  â•‘  Data de criaÃ§Ã£o: 31/05/2025                               â•‘
  â•‘  VersÃ£o: 1.1.3                                             â•‘
  â•‘  LicenÃ§a: MIT                                              â•‘
  â•šâ•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•â•
*/

/*
  RECADOS

  âš™ï¸ CONFIGURAÃ‡Ã•ES DE REDE

  Altere as variÃ¡veis abaixo conforme sua rede:
  - BROKER_IP     â†’ IP do broker MQTT
  - BROKER_PORT   â†’ Porta do broker
  - SSID          â†’ Nome da rede Wi-Fi
  - PASSWORD      â†’ Senha do Wi-Fi
*/

/*
  BIBLIOTECAS
*/


/*
  ConfiguraÃ§Ã£o de I2C (Inter-Integrated Circuit)
*/
#include <Wire.h> 

/*
  ConexÃ£o Wireless WiFi
*/
#include <WiFi.h> 

/*
  ManipulaÃ§Ã£o de MemÃ³ria Permanente
*/
#include <Preferences.h> 

/*
  ConexÃ£o NTP (Network Time Protocol)
*/
// #include <NTPClient.h>

#include <time.h>
/*
  Biblioteca do Sensor OxÃ­metro
*/
#include "MAX30105.h" 

/*
  VERIFICAR 
  USO 
  OQUE FAZ 
  SE PRESCISA
*/
#include "heartRate.h" //processamento de BPM?OxigenaÃ§Ã£o

/*
  Biblioteca do Sensor de Temperatura
*/
#include <Adafruit_MLX90614.h>

/*
  Biblioteca de Controle do Display
*/
#include <Adafruit_SSD1306.h>

/*
  Biblioteca GrÃ¡fica do Display

  VERIFICAR
  PARECE QUE NÃƒO TA SENDO USADA 
*/
#include <Adafruit_GFX.h>

/*
  Biblioteca do MQTT (Message Queuing Telemetry Transport)
*/
#include <PubSubClient.h>

/*
  Biblioteca de Formato JSON para IoT
  JSON (JavaScript Object Notation)
  IoT (Internet of Things)
*/
#include <ArduinoJson.h>

/*
  VARIÃVEIS DE BIBLIOTECAS
*/

/*
  INSTÃ‚NCIAS DE OBJETOS
*/

/*
  // -----[Preferences]----- //
  GerÃªncia o Armazenamento Dados de Forma Persistente na MemÃ³ria Flash (NVS - Non-Volatile Storage)
*/
Preferences prefs;

/*
  // -----[WiFi]----- //
  Permite ConexÃµes Wireless Possibilitando comunicar-se pela internet ou rede local
*/
WiFiClient espClient;

/*
  // -----[PubSubClient]----- //
  ComunicaÃ§Ã£o Via Protocolo MQTT, Permitindo que o Microcontrlador 
  Publique Dados (Como Sensores) e Receba Comandos de um Broker MQTT
*/
PubSubClient mqttClient(espClient);

/*
  // -----[I2C]----- //
  Permite a CriaÃ§Ã£o de Mais de um Barramento I2C Funcionando ao Mesmo Tempo
  VERIFICAR
*/ 
TwoWire I2C_0 = TwoWire(0);

//REMOVER DEPOIS
//TwoWire I2C_1 = TwoWire(1);



/*
  // -----[MAX30105-OxÃ­metro]----- //
  Objeto do Sensor MAX30105 para Leitura de SpO2 e BPM
*/
MAX30105 particleSensor;

/*
  // -----[MLX90614-Temperatura]----- //
  Objeto do Sensor MLX90614 para Leitura da Temperatura
*/
Adafruit_MLX90614 mlx = Adafruit_MLX90614();

/*
  // -----[Display-OLED]----- //
  Objeto do Display OLED para Exibir os Dados no Display
*/
Adafruit_SSD1306 display(128, 64, &I2C_0, -1);


/*
  // -----[NTPClient]----- //
  VERIFICAR
  NÃƒO SEI COMO TA USANDO SE TA USANDO AQUI CHAMDNO LA EMBAIXO
*/
// ConfiguraÃ§Ã£o do NTP
// WiFiUDP ntpUDP;
// NTPClient timeClient(ntpUDP, "pool.ntp.org", -3 * 3600, 60000); // UTC-3 (Brasil)



/*
  VARIÃVEIS DE GLOBAIS
*/


/*
  // -----[Wifi]----- //
  SSID - da Rede Wifi
  PASSWORD - Senha da Rede Wifi
*/
const char* ssid = "777zip";
const char* password = "R125redes";

// const char* ssid = "TP-Link_0486";
// const char* password = "46179951";

// DefiniÃ§Ãµes do Broker MQTT
// const char* ip_broker = "192.168.1.105";  // Ou IP do seu broker local


/*
  // -----[Broker]----- //
  IP_BROKER - Ip do ServiÃ§o do Broker
  BROKER_PORT - Porta do ServiÃ§o do Broker

  VERIFICAR ACHO QUE TEM QUE GERAR DEPOIS
  TEM QUE SER UNICO AGEITAR URGENTE
  CLIENT_ID - Indentificador Ãšnico de ConexÃ£o com Broker
*/
const char* ip_broker = "10.67.23.44";  // Ou IP do seu broker local
const int broker_port = 1883;
String client_id;


/*
  // -----[TÃ³picos]----- //
  PUB - TÃ³picos de PublicaÃ§Ã£o
  SUB - TÃ³picos de SubscriÃ§Ã£o
  REQUEST_USER - RequisiÃ§Ã£o de UsuÃ¡rio
  RESPONSE_USER - Recebe Novo UsuÃ¡rio
  MSG - Recebe Mensagens
*/
String topic_pub_request_user;
String topic_sub_response_user;
String topic_sub_msg;

/*
  // -----[MAC]----- //
  Declara a VariÃ¡vel que Recebera o EndereÃ§o MAC do Dispositivo
*/
String mac_address;

/*
  // -----[UsuÃ¡rio]----- //
  Declara a VariÃ¡vel que Recebera o Id do Relacionamento do Esp com o UsuÃ¡rio
*/
String user;

/*
  // -----[Dados]----- //
*/
const char* tipos[] = { "temperatura", "oxigenacao", "bpm" };

unsigned long tempoAnterior = 0;
const unsigned long intervalo = 5000; // 5 segundos


/*
  // -----[OxÃ­metro]----- //
  VariÃ¡veis para CÃ¡lculos spO2 e BPM
  VariÃ¡veis de Setup
*/
double avered = 0;
double aveir = 0;
double sumirrms = 0;
double sumredrms = 0;
int i = 0;
int Num = 200; //intervalo de amostragem para o cÃ¡lculo de SpO2
double ESpO2 = 95.0; // valor inicial de SpO2 estimado
double FSpO2 = 0.7;  // fator de filtro para SpO2 estimado
double frate = 0.95; // filtro passa-baixo para o valor do LED IR/vermelho

#define TIMETOBOOT 3000 // tempo de espera (ms) para a saÃ­da do SpO2
#define SCALE 88.0 // ajuste para exibir batimento cardÃ­aco e SpO2 na mesma escala
#define SAMPLING 5 // mais preciso se definido como 1
#define FINGER_ON 30000 // se o sinal vermelho for menor que isso, indica que o dedo nÃ£o estÃ¡ no sensor
#define MINIMUM_SPO2 80.0

const byte RATE_SIZE = 4; // Aumente isso para mais mÃ©dia. 4 Ã© bom.
byte rates[RATE_SIZE]; // Array de batimentos cardÃ­acos
byte rateSpot = 0;
long lastBeat = 0; // Hora em que ocorreu o Ãºltimo batimento
float beatsPerMinute;
int beatAvg;
double spo2 = 0.0;
double temp = 0.0;
double abpm = 0.0;
#define USEFIFO


/*
  // -----[Display]----- //
  Estados do Display
  Bitmaps
*/

// --- Estados --- //
unsigned long tempoMensagem = 0;
bool mostrandoMensagem = false;
unsigned long ultimoBatimento = 0;
bool batendo = false;

// --- Gota de Sangue (SpO2) --- //
const unsigned char epd_bitmap_gota_de_sangue [] PROGMEM = {
  0xff, 0xff, 0xfe, 0x7f, 0xfe, 0x7f, 0xfc, 0x3f,
  0xf8, 0x1f, 0xf0, 0x1f, 0xf0, 0x0f, 0xf0, 0x0f,
  0xe0, 0x07, 0xe0, 0x07, 0xe0, 0x07, 0xe0, 0x07,
  0xf0, 0x0f, 0xf0, 0x0f, 0xfc, 0x3f, 0xff, 0xff
};
// --- CoraÃ§Ã£o (BPM) --- //
const unsigned char epd_bitmap_coracao [] PROGMEM = {
  0xff, 0xff, 0xff, 0xff, 0xc3, 0xc3, 0x80, 0x01,
  0x80, 0x01, 0x00, 0x00, 0x80, 0x01, 0x80, 0x01,
  0x80, 0x01, 0xc0, 0x03, 0xe0, 0x07, 0xf0, 0x0f,
  0xfc, 0x3f, 0xfe, 0x7f, 0xff, 0xff, 0xff, 0xff
};
// --- TermÃ´metro (Temperatura) --- //
const unsigned char epd_bitmap_termometro [] PROGMEM = {
  0xfe, 0x7f, 0xfc, 0x3f, 0xf9, 0x9f, 0xf9, 0x9f,
  0xf9, 0x9f, 0xf9, 0x9f, 0xf8, 0x1f, 0xf8, 0x1f,
  0xf8, 0x1f, 0xf8, 0x1f, 0xf2, 0x4f, 0xf0, 0x0f,
  0xf0, 0x0f, 0xf2, 0x4f, 0xf8, 0x1f, 0xfc, 0x3f
};

/*
    FUNÃ‡Ã•ES DE SETUP
*/
/*
  // -----[setup_wifi]----- //
  FunÃ§Ã£o para conectar ao Wi-Fi
*/ 
void setup_wifi() {
    WiFi.begin(ssid, password);
    while (WiFi.status() != WL_CONNECTED) {
        delay(1000);
    }
}

/*
  // -----[mqttCallback]----- //
  Trata Mensagens Recebidas Via MQTT
  TOPIC - TÃ³pico em que a Mensagem foi Recebida
  PAYLOAD - Conteudo da Mensagem
  LENGTH - Tamanho da Mensagem em Bytes
*/

void mqttCallback(char* topic, byte* payload, unsigned int length) {

  //verifica se o topico e o correto
    if(strcmp(topic, topic_sub_response_user.c_str()) == 0){
        //joga o payload(usuario) dentro de uma variavel
        String novoUser;
        for (int i = 0; i < length; i++) {
            novoUser += (char)payload[i];
        }
        // Salvar novo valor do usuario permanentemente
        prefs.begin("config", false);
        prefs.putString("user", novoUser);
        prefs.end();
        // Atualiza o valor do usuÃ¡rio no codigo
        user = novoUser;
    }
  String msg = "";

  if (strcmp(topic, topic_sub_msg.c_str()) == 0) {
    for (int i = 0; i < length; i++) {
      msg += (char)payload[i];
    }
    tempoMensagem = millis();
    mostrandoMensagem = true;
    displayMensagem(msg);  // ou o que vocÃª quiser fazer com a mensagem
  }

    
}


/*
  // -----[reconnect_mqtt]----- //
  Verifica se a ConexÃ£o Esta Ativa
  Conecta ao BrokerMQTT 
  Subscreve nos TÃ³picos response_user e msg
*/
void reconnect_mqtt() {
    while (!mqttClient.connected()) {
        if (mqttClient.connect(client_id.c_str())) {
            mqttClient.subscribe(topic_sub_response_user.c_str()); 
            mqttClient.subscribe(topic_sub_msg.c_str());
        } else {
          delay(5000);
        }
    }
}


/*
  // -----[enviarPrimeiraMensagem]----- //
  Envia o Pedido do Primeiro UsuÃ¡rio
*/
void enviarPrimeiraMensagem() {
    while (!mqttClient.publish(topic_pub_request_user.c_str(), mac_address.c_str())) {
        delay(3000);
    }

}

/*
  // -----[initI2C]----- //
  Define as Pinos SDA e SCL
  Inicia o Barramento I2C 
*/
void initI2C() {
  I2C_0.begin(8, 9); //I2C 0 para o MAX30102 e Display Oled //TEMPERATURA
}


/*
  // -----[initDisplay]----- //
  Inicia o Display
  Limpa o Display
  Atualiza o Display
*/
void initDisplay() {
  display.begin(SSD1306_SWITCHCAPVCC, 0x3C);
  display.clearDisplay();
  display.display();
}


/*
  // -----[confMAX30102]----- //
  Configura o Sensor MAX30102/30105


  VERIFICAR PRA MIM FAZENDO A BOA

*/
void confMAX30102() {

  byte ledBrightness = 0x7F; //intensidade do led
  byte sampleAverage = 4; //amostras para mÃ©dia 
  byte ledMode = 3; //modo do led
  int sampleRate = 200; //frequÃªncia de amostragem (Hz)
  int pulseWidth = 411; //duraÃ§Ã£o de pulso do led
  int adcRange = 16384; // faixa de leitura adc
  
  particleSensor.setup(ledBrightness, sampleAverage, ledMode, sampleRate, pulseWidth, adcRange);
}


/*
  // -----[initSensors]----- //
  Inicia o Sensor de OxÃ­metro no Barramento I2C_0
  Inicia o Sensor de Temperatura no Barramento I2C_0
  Envia as ConfiguraÃ§Ãµes do OxÃ­metro
  Define a FrequÃªncia de ComunicaÃ§Ã£o do Barramento I2C
*/
void initSensors() {
  particleSensor.begin(I2C_0); //inicia sensor MAX30102 (velocidade padrÃ£o 100khz) 
  I2C_0.setClock(100000); //altere a velocidade entre 100/400 kHz
  confMAX30102();
  mlx.begin(0x5A, &I2C_0); //inicia o sensor MLX90614

}


/*
  // -----[drawHeart]----- //

  QUEM FEZ COMENTA AI RESUMO DOQUE FAZ 
*/
void drawHeart(float scale) {
  int centerX = 8;
  int centerY = 8;

  for (int y = 0; y < 16; y++) {
    for (int x = 0; x < 16; x++) {
      if (pgm_read_byte(&epd_bitmap_coracao[y * 2 + (x / 8)]) & (1 << (7 - (x % 8)))) {
        int dx = x - centerX;
        int dy = y - centerY;
        int sx = centerX + dx * scale;
        int sy = centerY + dy * scale;
        if (sx >= 0 && sx < 128 && sy >= 0 && sy < 64) {
          display.drawPixel(sx, sy, WHITE);
        }
      }
    }
  }
}

/*
  // -----[drawHeart]----- //
  Retorna a Hora e Minutos (HH:MM)
*/
String getHoraAtual() {
  time_t now = time(nullptr);
  struct tm* timeinfo = localtime(&now);
  char buffer[6];
  strftime(buffer, sizeof(buffer), "%H:%M", timeinfo);
  return String(buffer);
}


/*
    FUNÃ‡Ã•ES DE LEITURA
*/

/*
  // -----[readMAX]----- //
  Faz a Leitura e CÃ¡lculo do SpO2 e BPM
*/
void readMAX() {
 uint32_t ir, red;
 double fred, fir;
 double SpO2 = 0; // SpO2 bruto

#ifdef USEFIFO
 particleSensor.check(); // Verifica o sensor, lÃª atÃ© 3 amostras

 while (particleSensor.available()) {
  red = particleSensor.getFIFORed();
  ir = particleSensor.getFIFOIR();

 //CÃ¡lculo de BPM
  if (checkForBeat(ir)) {
   long delta = millis() - lastBeat;
   lastBeat = millis();
   beatsPerMinute = 60 / (delta / 1000.0);

   if (beatsPerMinute < 255 && beatsPerMinute > 20) {
    rates[rateSpot++] = (byte)beatsPerMinute;
    rateSpot %= RATE_SIZE;

    beatAvg = 0;
    for (byte x = 0; x < RATE_SIZE; x++) beatAvg += rates[x];
    beatAvg /= RATE_SIZE;
    abpm = beatAvg;
   }
  }

 //CÃ¡lculo de SpO2
  i++;
  fred = (double)red;
  fir = (double)ir;

  avered = avered * frate + red * (1.0 - frate);
  aveir = aveir * frate + ir * (1.0 - frate);
  sumredrms += (fred - avered) * (fred - avered);
  sumirrms += (fir - aveir) * (fir - aveir);

  if ((i % SAMPLING) == 0 && millis() > TIMETOBOOT) {
   if (ir < FINGER_ON) {
    ESpO2 = MINIMUM_SPO2;
   }
  }

  if ((i % Num) == 0) {
   double R = (sqrt(sumredrms) / avered) / (sqrt(sumirrms) / aveir);
   SpO2 = -23.3 * (R - 0.4) + 100;
   ESpO2 = FSpO2 * ESpO2 + (1.0 - FSpO2) * SpO2;

   sumredrms = 0.0;
   sumirrms = 0.0;
   i = 0;
   break;
  }

  particleSensor.nextSample();
 }
#endif

 spo2 = ESpO2;
}

/*
  // ------[readMLX]----- //
  Faz a Leitura da Temperatura
*/
void readMLX() {
 temp = mlx.readObjectTempC(); //leitura da temperatura corporal
}

/*
  // ------[displayOled]----- //
  Cria o CÃ©rebro Visual do Projeto
*/
void displayOled(bool beat) {
  display.clearDisplay();

  // Hora
  String hora = getHoraAtual();
  display.setCursor(90, 4);
  display.print(hora);

  // Batimento
  float scale = beat ? 0.8 : 1.0;
  drawHeart(scale);

  // BPM
  display.setTextSize(1);
  display.setTextColor(WHITE);
  display.setCursor(20, 4);
  display.print(abpm);
  display.print("BPM");

  // OxigÃªnio
  display.drawBitmap(0, 20, epd_bitmap_gota_de_sangue, 16, 16, WHITE);
  display.setCursor(20, 24);
  display.print(spo2);
  display.print("%");
  // Temperatura
  display.drawBitmap(0, 40, epd_bitmap_termometro, 16, 16, WHITE);
  display.setCursor(20, 44);
  display.print(temp);
  display.print("Â°C");

  display.display();
}


void atualizarTopicos(){
  topic_pub_request_user = mac_address + "/request_user";
  topic_sub_response_user = mac_address + "/response_user";
  topic_sub_msg = mac_address + "/msg";
}


/*
  // ------[displayMensagem]----- //
  Cria Display para o Evento de Mensagem
*/
void displayMensagem(String msg) {
  display.clearDisplay();
  display.setTextSize(1);
  display.setTextColor(WHITE);
  display.setCursor(0, 0);
  display.println("MENSAGEM:");
  display.setCursor(0, 16);
  display.println(msg);
  display.display();
}


/*
  VOID SETUP
  // ------[setup]----- //
  Inicia o Wifi
  Configura a Hora Interna do Esp
  Atualisa para o UsuÃ¡rio Salvo
  Captura o Mac Address do Esp
  Inicia as FunÃ§Ãµes de InicializaÃ§Ã£o dos Sensores, I2C e Display

  VERIFICAR
  TIRAR O SERIAL
  VERIFICA O NPT PRA FAZER O HORARIO INTERNO DO ESP FICAR CORETO
  PROVAVELMENTE REMOVER O NPT CLIENT E USAR SO O CONFIGTIME
  DESATIVAR O RESET DE USUARIO .remove()
  DEIXAR OS TOPICOS DINAMICOS
*/
void setup() {
  
  setup_wifi();
  mqttClient.setServer(ip_broker, broker_port);
  mqttClient.setCallback(mqttCallback);

  // timeClient.begin();
  // timeClient.update();
  configTime(-3 * 3600, 0, "pool.ntp.org");

  // Abrir namespace "config" no modo leitura/escrita
  prefs.begin("config", false);
  // temporario para testes
  //prefs.remove("user");
  // Ver se jÃ¡ existe valor salvo
  user = prefs.getString("user", "");
  mac_address = WiFi.macAddress();
  client_id = "ESP_"+ mac_address; 
  atualizarTopicos();
  initI2C(); //inicia o barramento I2C
  initDisplay(); //inicia o Display 
  initSensors(); //inicia os sensores
}

/*
    VOID LOOP
*/



/*
  // ------[loop]----- //
  Verifica a ConexÃ£o com broker
  Mantem a ConexÃ£o Viva e Processa Mensagens
  Verifica a ExistÃªncia do UsuÃ¡rio

  VERIVICAR
  Faz a  leitura dos sinais vitais e envia os dados
  FAZER UM FUNÃ‡ÃƒO DE RECONECT DO WIFI SE ELE NÃƒO OCNSEGUIR SE CONECTAR NO BROKER IF PRA VE SE TAR CONECTADO NO WIFI SE NÃƒO TENTA CONECTAR

*/
void loop() {
  if (!mqttClient.connected()) {
      if(WiFi.status() != WL_CONNECTED){
        setup_wifi();
      }
      reconnect_mqtt();  // sua funÃ§Ã£o de reconexÃ£o
    }
    mqttClient.loop();

    readMLX();
    readMAX();
    

    // Verifica se o user estÃ¡ vazio
    if (user == "") {
        static bool primeiraMensagemEnviada = false;
        //pede um novo usuÃ¡rio
        if (!primeiraMensagemEnviada) {
            enviarPrimeiraMensagem();
            primeiraMensagemEnviada = true;
        }
    } else {
        unsigned long tempoAtual = millis();

        // Se passaram 5 segundos?
        if (tempoAtual - tempoAnterior >= intervalo) {
            tempoAnterior = tempoAtual;
            // Aqui vocÃª coloca o que deseja fazer a cada 5 segundos
            for (int i = 0; i < 3; i++) {
            String valor;
            
            if (strcmp(tipos[i], "temperatura") == 0) {
                valor = String(temp, 2); // duas casas decimais

            } else if (strcmp(tipos[i], "oxigenacao") == 0) {
                valor = String(spo2, 2);
                
            } else if (strcmp(tipos[i], "bpm") == 0) {
                valor = String(abpm);

            }

            StaticJsonDocument<200> doc;
            doc["use_id"] = user;
            doc["dados_tipo"] = tipos[i];
            doc["dados_valor"] = valor;
            doc["dados_generate"] = time(NULL);

            String e;
            serializeJson(doc, e);

            String topico = mac_address+ "/" + tipos[i];
            // ğŸš€ Verifica se a mensagem foi publicada com sucesso
            mqttClient.publish(topico.c_str(), e.c_str());
        }
      }
    }
    
  if (mostrandoMensagem && millis() - tempoMensagem >= 10000) {
    mostrandoMensagem = false;
    display.clearDisplay();
  }

  if (!mostrandoMensagem && millis() - ultimoBatimento >= 500) {
    batendo = !batendo;
    displayOled(batendo);
    ultimoBatimento = millis();
  }
}
